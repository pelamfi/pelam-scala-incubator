plugins {
    // As documented in https://openjfx.io/openjfx-docs/#gradle
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'maven'
    id 'scala'
    id 'idea'
}

if (Boolean.valueOf(signArtifacts)) {
    println "Artifact signing is on."
    apply plugin: 'signing'
}

javafx {
    version = "14"
    modules = [ 'javafx.controls' ]
}

group = 'fi.pelam'
version = '0.5-SNAPSHOT'
description = """
Pelam's open source utility code incubator and collection.
"""

repositories {

    mavenLocal()
    mavenCentral()

}

dependencies {
    compile 'org.openjfx:javafx:14.0.1'

    compile 'org.scala-lang:scala-library:2.12.8'
    compile 'org.scala-lang:scala-reflect:2.12.8'

    // For Java like enums with small amount of
    // boilerplate https://github.com/lloydmeta/enumeratum
    compile 'com.beachape:enumeratum_2.12:1.5.15'

    compile 'com.typesafe.akka:akka-actor_2.12:2.6.4'

    // http://stackoverflow.com/a/21219051/1148030
    compile 'com.google.code.findbugs:jsr305:3.0.0'

    compile 'com.googlecode.java-diff-utils:diffutils:1.3.0'
    compile 'com.google.guava:guava:29.0-jre'

    compile 'org.clapper:grizzled-slf4j_2.12:1.3.4'
    testImplementation 'ch.qos.logback:logback-classic:1.2.3'

    testImplementation 'junit:junit:4.12'
}

if (Boolean.valueOf(signArtifacts)) {
    signing {
        sign configurations.archives
    }
}

// http://stackoverflow.com/a/11475089/1148030
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task scaladocJar(type: Jar, dependsOn: scaladoc) {
    // ScalaDoc is kept in -javadoc jars http://www.scala-lang.org/old/node/12471.html
    classifier = 'javadoc'
    from scaladoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives scaladocJar
}

uploadArchives {
    dependsOn(scaladocJar)
    dependsOn(sourcesJar)
    repositories {
        mavenDeployer {
            pom.project {
                name "Pelam's Scala Utility Incubator Library"
                description = project.description
                url "https://github.com/pelamfi/pelam-scala-incubator/blob/master/readme.md"
                inceptionYear 2016
                scm {
                    connection "scm:git:https://github.com/pelamfi/pelam-scala-incubator.git"
                    url "https://github.com/pelamfi/pelam-scala-incubator"
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'pelam'
                        name = 'Peter Lamberg'
                    }
                }
            }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: sonatypeUser, password: sonatypePassword)
            }
            if (Boolean.valueOf(signArtifacts)) {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }
            }
        }
    }
}

tasks.withType(ScalaDoc) {
    title = "Pelam's Scala Util Incubator library"

    scalaDocOptions.additionalParameters = [
            // TODO: convert readme.md to html and feed to scaladoc here? "-doc-root-content", "readme.???",
            "-sourcepath", projectDir.toString() + "/src/main/scala",
            "-doc-source-url", "https://github.com/pelamfi/pelam-scala-incubator/blob/master/src/main/scalaâ‚¬{FILE_PATH}.scala"]
}

idea {
    module {
        excludeDirs += file(".idea")
    }
}